# Contributor: Anthony Wang <ta180m at pm dot me>
# Contributor: Chris Lane <aur at chrislane dot com>
_pkgname=digikam
pkgname=digikam-git
pkgver=v7.5.0.r2192.gfc2f44b459
pkgrel=1
pkgdesc='An advanced digital photo management application'
arch=('i686' 'x86_64')
license=('GPL')
url="https://www.digikam.org/"
depends=(lensfun opencv knotifyconfig libksane kfilemetadata marble-common
         threadweaver kcalendarcore qt5-xmlpatterns glu jasper)
makedepends=(extra-cmake-modules doxygen eigen boost kdoctools git mold ccache ninja)
optdepends=('hugin: panorama tool'
            'qt5-imageformats: support for additional image formats (WEBP, TIFF)'
            'rawtherapee: RAW import'
            'darktable: RAW import'
            'perl: for digitaglinktree')
conflicts=('digikam')
provides=('digikam')
# source=('git+https://invent.kde.org/graphics/digikam.git#branch=qt5-maintenance'
source=('git+https://invent.kde.org/graphics/digikam.git#branch=master'
        'lbpcascade_animeface.patch')
sha256sums=('SKIP'
            'SKIP')
#             'SKIP')
pkgver() {
  cd ${_pkgname}
  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {

  cd ${_pkgname}
  patch --forward --strip=1 --input="${srcdir}/lbpcascade_animeface.patch"
}

build() {
  export CC='ccache gcc'
  export CXX='ccache g++'
  cmake \
      -B build \
      -S ${_pkgname} \
      -DCMAKE_BUILD_TYPE=relwithdebinfo \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DCMAKE_DISABLE_FIND_PACKAGE_ImageMagick=True \
      -DCMAKE_DISABLE_FIND_PACKAGE_Qt6=True \
      -DBUILD_TESTING=OFF \
      -DENABLE_QWEBENGINE=ON \
      -DENABLE_KFILEMETADATASUPPORT=ON \
      -DENABLE_MEDIAPLAYER=ON \
      -DENABLE_AKONADICONTACTSUPPORT=OFF \
      -DENABLE_MYSQLSUPPORT=ON \
      -DENABLE_APPSTYLES=ON \
      -Wno-dev \
      -G Ninja
#       -DCMAKE_PREFIX_PATH="/usr/lib/ffmpeg4.4;/usr/include/ffmpeg4.4" \
#       -DBUILD_WITH_QT6=ON \
  mold -run ninja -j 4 -C build
}

package() {
  DESTDIR="${pkgdir}" ninja install -C build
}

# vim:set ts=2 sw=2 et:
